#!/bin/env -S gawk -f

# Cron executable to check for new signal messages and notify
# Cam Webb <https://github.com/camwebb/signal-cli-wrapper>
# This software is released under GNU GPLv3. See LICENSE file.

# Installation:
#   1) Make this script executable
#   2) This script and signal-cli must be in your shell's $PATH
#   3) Make sure "scw_config.awk" is in a directory present in environment
#      variable $AWKPATH
#   4) trigger via crontab, e.g.,
#        0,10,20,30,40,50 * * * *   AWKPATH=/home/foo/awklib \
#                                   /home/foo/bin/checksig   \
#                                   /home/foo/bin/sig

@include "/home/cam/lib/awk-libs/scw_config.awk"

BEGIN{

  # Setup
  config()
  proc_nums()

  # test to see if you are online
  "ping -c 1 google.com 2> /dev/null | grep '1 received'" | getline CK
  if (!CK)
    exit 1

  # if not run as a user the executable should be set as the first argument
  # if (ARGC == 2)
  #   SIG = ARGV[1]
  # else
  SIG = "/home/cam/bin/signal"
  
  cmd = SIG " rcv"
  RS  = ""
  FS  = "\n"
  ORS = ""
  while ((cmd | getline) > 0) {
    sender = body = ""
    for (i = 1; i <= NF; i++) {
      if ($i ~ /^Sender:/)
        sender = gensub(/^[^+]+(\+[0-9]+) .*$/,"\\1","G",$i)
      else if (($i ~ /^Body:/) && sender && (sender != MYNUM)) {
        if (iNUM[sender])
          list[iNUM[sender]]++
        else
          list[sender]++
      }
    }
  }
  
  PROCINFO["sorted_in"] = "@ind_str_asc"
  if (isarray(list)) {
    msg = ""
    for (i in list)
      msg = msg " " i " (" list[i] ")"
  }
  
  if (msg) {
    system("DBUS_SESSION_BUS_ADDRESS='unix:path=/run/user/1000/bus' "   \
           "notify-send -u normal -t 36000000 'Signal messages from:" msg "'")
    # system("sudo /root/bin/msgterm 'Signal msg from " msg "'")
  }
}

# this is a copy of the same function in `sig`
function proc_nums(    i, n1, n2, nm, np) {
  gsub(/ +/,"",NUMS)
  gsub(/;$/,"",NUMS)
  split(NUMS, n1, ";")
  for (i in n1) {
    split(n1[i], n2, ":")
    nm[n2[1]]++
    np[n2[2]]++
    if((nm[n2[1]] > 1) || (np[n2[2]] > 1)) {
      print "Duplicate entry: " n1[i] > "/dev/stderr"
      exit 1
    }
    NUM[n2[1]] = n2[2]
    iNUM[n2[2]] = n2[1]
    if (n2[1] == MYNAME)
      MYNUM = n2[2]
  }
  if (!NUM[MYNAME]) {
    print "No MYNAME in config file" > "/dev/stderr"
    exit 1
  }
  # create SCLI
  SCLI = "/home/" ENVIRON["USER"] "/.local/share/signal-cli/data/" MYNUM ".d/"
}
